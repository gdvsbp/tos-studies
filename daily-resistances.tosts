

def YesterdayHigh = high(period = "day")[1];
def YesterdayLow = low(period = "day")[1];
def YesterdayOpen = open(period = "day")[1];
def YesterdayClose = close(period = "day")[1];

def TodayHigh = high(period = "day")[0];
def TodayLow = low(period = "day")[0];
def TodayOpen = open(period = "day")[0];

AddLabel(yes, "Yesterday", Color.WHITE);

plot PlotYesterdayOpen = HighestAll(if IsNaN(open[-1]) and !IsNaN(open) then YesterdayOpen else Double.NaN);
PlotYesterdayOpen.SetDefaultColor(Color.DARK_GREEN);
AddLabel(PlotYesterdayOpen, "↦ $" + PlotYesterdayOpen, Color.DARK_GREEN);

plot PlotYesterdayHigh = HighestAll(if IsNaN(high[-1]) and !IsNaN(high) then YesterdayHigh else Double.NaN);
PlotYesterdayHigh.SetDefaultColor(Color.DARK_ORANGE);
AddLabel(PlotYesterdayHigh, "↑ $" + PlotYesterdayHigh, Color.DARK_ORANGE);

plot PlotYesterdayLow = HighestAll(if IsNaN(low[-1]) and !IsNaN(low) then YesterdayLow else Double.NaN);
PlotYesterdayLow.SetDefaultColor(Color.PLUM);
AddLabel(PlotYesterdayLow, "↓ $" + PlotYesterdayLow, Color.PLUM);

plot PlotYesterdayClose = HighestAll(if IsNaN(close[-1]) and !IsNaN(close) then YesterdayClose else Double.NaN);
PlotYesterdayClose.SetDefaultColor(Color.DOWNTICK);
AddLabel(PlotYesterdayClose, "⇥ $" + PlotYesterdayClose, Color.DOWNTICK);



# Archive Name GlobeX or Overnight High / Low v.07.2017
# Archive Section Support Resistance
# Suggested Tos Name GlobeX_HighLow_v2017_Mobius
# Archive Date 5.06.2018 May 6, 2018
# Archive Notes This looks to be a second version

# GlobeX or Overnight High / Low
# Mobius
# V01.2012
# V07.2017 Replaced Time Brackets with GetTime() function which works better with Mobile

input PlotOverNightExtremes = yes;

def h = high;
def l = low;
def bar = BarNumber();
def GlobeX = GetTime() < RegularTradingStart(GetYYYYMMDD());
def ONhigh = if GlobeX and !Globex[1]
             then h
             else if Globex and
                     h > ONhigh[1]
                  then h
                  else ONhigh[1];
def ONhighBar = if GlobeX and h == ONhigh
                then Bar
                else double.nan;
def ONlow = if GlobeX and !GlobeX[1]
            then l
            else if GlobeX and
                    l < ONlow[1]
                 then l
                 else ONlow[1];
def ONlowBar = if GlobeX and l == ONlow
               then Bar
               else double.nan;
def OverNightHigh = if BarNumber() == HighestAll(ONhighBar)
                    then ONhigh
                    else OverNightHigh[1];
def OverNightLow = if BarNumber() == HighestAll(ONlowBar)
                   then ONlow
                   else OverNightLow[1];
plot ONH = if OverNightHigh > 0
           then OverNightHigh
           else Double.NaN;
ONH.SetHiding(!PlotOverNightExtremes);
ONH.SetDefaultColor(CreateColor(100, 178, 255));
ONH.SetPaintingStrategy(PaintingStrategy.POINTS);
ONH.HideBubble();
ONH.HideTitle();
plot ONL = if OverNightLow > 0
           then OverNightLow
           else Double.NaN;
ONL.SetHiding(!PlotOverNightExtremes);
ONL.SetDefaultColor(CreateColor(50, 128, 255));
ONL.SetPaintingStrategy(PaintingStrategy.POINTS);
ONL.HideBubble();
ONL.HideTitle();
# AddLabel(GlobeX, "GlobeX Range = " + AsDollars(ONH - ONL), color.white);
# AddChartBubble(bar == ONhighBar and PlotOverNightExtremes, ONH, "ONH", Color.CYAN);
# AddChartBubble(bar == ONlowBar and PlotOverNightExtremes, ONL, "ONL", Color.BLUE, 0);
Alert((close crosses above ONH) or (close crosses below ONL), "", Alert.Bar, Sound.Bell);
# End Code GlobeX or Overnight High / Low v.07.2017
AddLabel(yes, "Overnight", Color.WHITE);
AddLabel(ONH, "↑ $" + ONH, CreateColor(100, 178, 255));
AddLabel(ONL, "↓ $" + ONL, CreateColor(50, 128, 255));

AddLabel(yes, "Today", Color.WHITE);

plot PlotTodayOpen = HighestAll(if IsNaN(open[-1]) and !IsNaN(open) then TodayOpen else Double.NaN);
PlotTodayOpen.SetDefaultColor(Color.GREEN);
PlotTodayOpen.SetLineWeight(2);
AddLabel(PlotTodayOpen, "↦ $" + PlotTodayOpen, Color.GREEN);

plot PlotTodayHigh = HighestAll(if IsNaN(high[-1]) and !IsNaN(high) then TodayHigh else Double.NaN);
PlotTodayHigh.SetDefaultColor(Color.ORANGE);
PlotTodayHigh.SetLineWeight(2);
AddLabel(PlotTodayHigh, "↑ $" + PlotTodayHigh, Color.ORANGE);

plot PlotTodayLow = HighestAll(if IsNaN(low[-1]) and !IsNaN(low) then TodayLow else Double.NaN);
PlotTodayLow.SetDefaultColor(Color.MAGENTA);
PlotTodayLow.SetLineWeight(2);
AddLabel(PlotTodayLow, "↓ $" + PlotTodayLow, Color.MAGENTA);