declare LOWER;

input price = close;
def vol = volume;

input Price_MA_Average_Type = AverageType.SIMPLE;
input Price_MA_Length = 9; # 9 for price, 20 for vol
input Price_RSI_Length = 13; # 13 for price, 20 for vol

input Vol_MA_Average_Type = AverageType.SIMPLE;
input Vol_MA_Length = 20; # 9 for price, 20 for vol
input Vol_RSI_Length = 20; # 13 for price, 20 for vol

input RSI_Average_Type = AverageType.EXPONENTIAL;
input Regular_RSI_Average_Type = AverageType.WILDERS;

input over_Bought = 95;
input over_Sold = 5;

input over_Bought_Minor = 80;
input over_Sold_Minor = 20;

def volOffset = -100;

script GetRSI {
  input avg = double.NaN;
  input myVal = 0;
  input myValLast = 0;
  input length = 0;
  def NetChgAvg = MovingAverage(avg, myVal - myValLast, length);
  def TotChgAvg = MovingAverage(avg, AbsValue(myVal - myValLast), length);
  def ChgRatio = if TotChgAvg != 0 then NetChgAvg / TotChgAvg else 0;

  plot rsi = 50 * (ChgRatio + 1);
}

# calc RSI of Price MA
def priceMaOutput = MovingAverage(Price_MA_Average_Type, price, Price_MA_Length);

# plot RSI of Price MA
plot Price_MA_Of_RSI = GetRSI(RSI_Average_Type, priceMaOutput, priceMaOutput[1], Price_RSI_Length);
Price_MA_Of_RSI.setLineWeight(2);
Price_MA_Of_RSI.setDefaultColor(Color.ORANGE);

# plot Price RSI
plot Price_RSI = GetRSI(Regular_RSI_Average_Type, price, price[1], Price_RSI_Length);
Price_RSI.setDefaultColor(Color.GRAY);

plot Price_Over_Bought = over_Bought;
Price_Over_Bought.setDefaultColor(Color.DARK_GREEN);

plot Price_Over_Bought_Minor = over_Bought_Minor;
Price_Over_Bought_Minor.setDefaultColor(Color.DARK_GRAY);
Price_Over_Bought_Minor.setStyle(Curve.SHORT_DASH);

plot Price_Over_Sold = over_Sold;
Price_Over_Sold.setDefaultColor(Color.DARK_RED);

plot Price_Over_Sold_Minor = over_Sold_Minor;
Price_Over_Sold_Minor.setDefaultColor(Color.DARK_GRAY);
Price_Over_Sold_Minor.setStyle(Curve.SHORT_DASH);

# calc RSI of Volume MA
def volMaOutput = MovingAverage(Vol_MA_Average_Type, vol, Vol_MA_Length);

# plot RSI of Vol MA
plot Vol_MA_Of_RSI = GetRSI(RSI_Average_Type, volMaOutput, volMaOutput[1], Vol_RSI_Length) + volOffset;
Vol_MA_Of_RSI.setLineWeight(2);
Vol_MA_Of_RSI.setDefaultColor(Color.CYAN);

# plot Vol RSI
plot Vol_RSI = GetRSI(Regular_RSI_Average_Type, vol, vol[1], Vol_RSI_Length) + volOffset;
Vol_RSI.setDefaultColor(Color.GRAY);

plot Vol_Over_Bought = over_Bought + volOffset;
Vol_Over_Bought.setDefaultColor(Color.DARK_GREEN);

plot Vol_Over_Sold = over_Sold + volOffset;
Vol_Over_Sold.setDefaultColor(Color.DARK_RED);
