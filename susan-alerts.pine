// @version=5
indicator("Susan Alerts", "", overlay = true)

// configurations
var ATR_THRESHOLD = 2.5

// indicators
[macd, signal, hist] = ta.macd(close, 12, 26, 9)
ema_5 = ta.ema(close, 5)
sma_15 = ta.sma(close, 15)
sma_100 = ta.sma(close, 100)
sma_200 = ta.sma(close, 200)
rsi = ta.rsi(close, 14)
atr = ta.atr(14)

// ***** Susan Peak & Valley Functions ***** //

calcPeaks(high) =>
    if ((high[2] < high[1] and high[0] < high[1]) or (high[3] < high[2] and high[2] == high[1] and high[0] < high[1]) or (high[4] < high[3] and high[3] == high[1] and high[2] == high[1] and high[0] < high[1]))
        high[1]
    else
        na

calcValley(low) =>
    if (low[2] > low[1] and low[0] > low[1])
        low[1]
    else
        na
// ****** ************************ ********** //

// store off latest peaks and valleys
var float peak = na
var float valley = na

peaksV = calcPeaks(high)
valleyV = calcValley(low)

if (peaksV)
    peak := high[1]
if (valleyV)
    valley := low[1]

// crossovers
smaCrossOver = ta.crossover(ema_5, sma_15)
smaCrossUnder = ta.crossunder(ema_5, sma_15)
macdCrossOver = ta.crossover(macd, signal)
macdCrossUnder = ta.crossunder(macd, signal)

var int smaCrossIndex = na
var int smaCrossUnderIndex = na
var int macdCrossIndex = na
var int macdCrossUnderIndex = na

if (smaCrossOver)
    smaCrossIndex := bar_index

if (macdCrossOver)
    macdCrossIndex := bar_index

if (smaCrossUnder)
    smaCrossUnderIndex := bar_index

if (macdCrossUnder)
    macdCrossUnderIndex := bar_index

var float longTakeProfitStep0 = na
var float longTakeProfitStep1 = na
var float longTakeProfitStep2 = na
var float longTakeProfitStep3 = na
var float longTakeProfitStep4 = na

var float shortTakeProfitStep0 = na
var float shortTakeProfitStep1 = na
var float shortTakeProfitStep2 = na
var float shortTakeProfitStep3 = na
var float shortTakeProfitStep4 = na

// function to format the alert to discord JSON style
generateDiscordMsg(msg) =>
	'{ "username": "Susan", "content": "' + msg + '" }'

// 4hrs are swings and any other are day trade for now
tradeType = timeframe.period == "240" ? "Swing" : "Day Trade"

// scaffold out a base discord message to send
base_msg = "**" + tradeType + "**" + "\n"
base_msg := base_msg + "Stock: " + syminfo.ticker + "\n"
base_msg := base_msg + "- Entry: " + str.tostring(close) + "\n"

if ((bar_index - smaCrossIndex) <= 5 or (bar_index - macdCrossIndex) <= 5)
    if (smaCrossIndex and macdCrossIndex)
        if ((valley + atr * ATR_THRESHOLD) >= close)
            if ((macdCrossIndex >= smaCrossIndex and (macdCrossIndex - smaCrossIndex) <= 5)) or ((smaCrossIndex > macdCrossIndex and (smaCrossIndex - macdCrossIndex) <= 5))
                longTakeProfitStep1 := close + atr * 1
                longTakeProfitStep2 := close + atr * 2
                longTakeProfitStep3 := close + atr * 3
                longTakeProfitStep4 := close + atr * 5

                buy_message = base_msg + "- Stop: " + str.tostring(valley) + "\n"
                buy_message := buy_message + "- Targets: " +  str.tostring(longTakeProfitStep1) + ", " + str.tostring(longTakeProfitStep2) + ", " + str.tostring(longTakeProfitStep3) + ", " + str.tostring(longTakeProfitStep4)
                
                alert_message = generateDiscordMsg(buy_message)

                alert(alert_message, alert.freq_once_per_bar_close)

                smaCrossIndex := na
                macdCrossIndex := na
else
    smaCrossIndex := na
    macdCrossIndex := na

if ((bar_index - smaCrossUnderIndex) <= 5 or (bar_index - macdCrossUnderIndex) <= 5)
    if (smaCrossUnderIndex and macdCrossUnderIndex)
        if ((peak - atr * ATR_THRESHOLD) <= close)
            if ((macdCrossUnderIndex >= smaCrossUnderIndex and (macdCrossUnderIndex - smaCrossUnderIndex) <= 5)) or ((smaCrossUnderIndex >= macdCrossUnderIndex and (smaCrossUnderIndex - macdCrossUnderIndex) <= 5))
                shortTakeProfitStep1 := close - atr * 1
                shortTakeProfitStep2 := close - atr * 2
                shortTakeProfitStep3 := close - atr * 3
                shortTakeProfitStep4 := close - atr * 5

                short_message = base_msg + "- Stop: " + str.tostring(peak) + "\n"
                short_message := short_message + "- Targets: " +  str.tostring(shortTakeProfitStep1) + ", " + str.tostring(shortTakeProfitStep2) + ", " + str.tostring(shortTakeProfitStep3) + ", " + str.tostring(shortTakeProfitStep4)
                
                alert_message = generateDiscordMsg(short_message)

                alert(alert_message, alert.freq_once_per_bar_close)
                
                smaCrossUnderIndex := na
                macdCrossUnderIndex := na
else
    smaCrossUnderIndex := na
    macdCrossUnderIndex := na
