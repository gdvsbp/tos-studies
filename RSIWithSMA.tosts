#
# TD Ameritrade IP Company, Inc. (c) 2007-2019
# Tweaked by @korygill
# https://usethinkscript.com/d/185-moving-average-crossover-rsi-indicator-for-thinkorswim

declare lower;

input length = 14;
input over_Bought = 70;
input over_Sold = 30;
input price = close;
input averageType = AverageType.WILDERS;
input showBreakoutSignals = no;
input rsiMALength = 5; #hint rsiMALength: RSI Moving Average Length
input rsiAverageType = AverageType.SIMPLE;
input rsiCrossDown = 60;
input rsiCrossUp = 40;

// @ts-ignore
def NetChgAvg = MovingAverage(averageType, price - price[1], length);
def TotChgAvg = MovingAverage(averageType, AbsValue(price - price[1]), length);
def ChgRatio = if TotChgAvg != 0 then NetChgAvg / TotChgAvg else 0;

plot RSI = 50 * (ChgRatio + 1);
plot OverSold = over_Sold;
plot OverBought = over_Bought;
plot UpSignal = if RSI crosses above OverSold then OverSold else Double.NaN;
plot DownSignal = if RSI crosses below OverBought then OverBought else Double.NaN;

# plot the RSI Moving Average
def rsiMA = MovingAverage(rsiAverageType, RSI, rsiMALength);
plot prsiMA = rsiMA;

plot XoverSignalUp = if (RSI crosses above rsiMA and rsiMA <= rsiCrossUp) then rsiMA else Double.NaN;
plot XoverSignalDn = if (RSI crosses below rsiMA and rsiMA >= rsiCrossDown) then rsiMA else Double.NaN;

Alert((RSI crosses above rsiMA and rsiMA <= rsiCrossUp) or (RSI crosses below rsiMA and rsiMA >= rsiCrossDown), "RSI SMA crossover. RSI: " + RSI + ", SMA: " + rsiMA, Alert.Bar, Sound.Bell);

UpSignal.SetHiding(!showBreakoutSignals);
DownSignal.SetHiding(!showBreakoutSignals);

RSI.DefineColor("OverBought", GetColor(5));
RSI.DefineColor("Normal", GetColor(7));
RSI.DefineColor("OverSold", GetColor(1));
RSI.AssignValueColor(if RSI > over_Bought then RSI.color("OverBought") else if RSI < over_Sold then RSI.color("OverSold") else RSI.color("Normal"));
OverSold.SetDefaultColor(GetColor(8));
OverBought.SetDefaultColor(GetColor(8));
UpSignal.SetDefaultColor(Color.UPTICK);
UpSignal.SetPaintingStrategy(PaintingStrategy.ARROW_UP);
DownSignal.SetDefaultColor(Color.DOWNTICK);
DownSignal.SetPaintingStrategy(PaintingStrategy.ARROW_DOWN);

XoverSignalUp.SetDefaultColor(Color.WHITE);
XoverSignalUp.SetPaintingStrategy(PaintingStrategy.ARROW_UP);
XoverSignalDn.SetDefaultColor(Color.WHITE);
XoverSignalDn.SetPaintingStrategy(PaintingStrategy.ARROW_DOWN);
